plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'jacoco'
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

group 'io.modicon'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation "io.jsonwebtoken:jjwt-api:${jwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${jwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jwtVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation "com.github.ben-manes.caffeine:caffeine:${caffeineVersion}"

    implementation 'commons-io:commons-io:2.11.0'

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${openApiVersion}"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testRuntimeOnly "com.h2database:h2:${h2Version}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

// integration tests setup
testing {
    suites {
        intTest(JvmTestSuite) {
            dependencies {
                implementation project
            }
        }
    }
}
dependencies {
    intTestImplementation 'org.springframework.security:spring-security-test'
    intTestCompileOnly 'org.projectlombok:lombok'
    intTestAnnotationProcessor 'org.projectlombok:lombok'
    intTestRuntimeOnly "com.h2database:h2:${h2Version}"
    intTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    intTestImplementation "org.springframework.cloud:spring-cloud-starter-openfeign:${openFeignVersion}"
    intTestImplementation "io.github.openfeign:feign-jackson:${jacksonFeignVersion}"
    intTestImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.2'
}

tasks.named('check') {
    dependsOn testing.suites.intTest
}

jacocoTestReport {
    dependsOn test
}